load("@io_bazel_rules_go//go:def.bzl", "go_binary", "go_library", "go_test")
load("@rules_proto//proto:defs.bzl", "proto_descriptor_set")
load("//:platforms.bzl", "platforms")

# gazelle:ignore testdata

go_library(
    name = "protoc-gen-starlark_lib",
    srcs = ["main.go"],
    embedsrcs = ["plugin_descriptor.pb"],
    importpath = "github.com/stackb/grpc-starlark/cmd/protoc-gen-starlark",
    visibility = ["//visibility:private"],
    deps = ["//pkg/plugin"],
)

go_binary(
    name = "protoc-gen-starlark",
    embed = [":protoc-gen-starlark_lib"],
    visibility = ["//visibility:public"],
)

[
    go_binary(
        name = "protoc-gen-starlark_%s_%s" % (
            platform.os,
            platform.arch,
        ),
        embed = [":protoc-gen-starlark_lib"],
        gc_linkopts = platform.gc_linkopts,
        goarch = platform.arch,
        goos = platform.os,
        pure = "on",
        static = "on",
        visibility = ["//visibility:public"],
    )
    for platform in platforms
]

[
    genrule(
        name = "protoc-gen-starlark_%s_%s_zip" % (
            platform.os,
            platform.arch,
        ),
        srcs = ["protoc-gen-starlark_%s_%s" % (
            platform.os,
            platform.arch,
        )],
        outs = ["protoc-gen-starlark_%s_%s.zip" % (
            platform.os,
            platform.arch,
        )],
        cmd = """
    cp $(location protoc-gen-starlark_{os}_{arch}) protoc-gen-starlark
    $(location @bazel_tools//tools/zip:zipper) c $@ protoc-gen-starlark
    """.format(
            arch = platform.arch,
            os = platform.os,
        ),
        tools = ["@bazel_tools//tools/zip:zipper"],
        visibility = ["//visibility:public"],
    )
    for platform in platforms
]

go_test(
    name = "protoc-gen-starlark_test",
    srcs = ["main_test.go"],
    data = glob(["testdata/**"]) + [
        "protoc.exe",
        "protoc-gen-unittest",
        "unittest_descriptor",
    ],
    deps = [
        "@com_github_google_go_cmp//cmp",
        "@net_starlark_go//lib/time",
    ],
)

genrule(
    name = "protoc_exe",
    srcs = ["@com_google_protobuf//:protoc"],
    outs = ["protoc.exe"],
    cmd = "cp $< $@",
    executable = True,
)

genrule(
    name = "protoc-gen-unittest_bin",
    srcs = [":protoc-gen-starlark"],
    outs = ["protoc-gen-unittest"],
    cmd = "cp $< $@",
    executable = True,
)

proto_descriptor_set(
    name = "unittest_descriptor",
    deps = ["@protoapis//google/protobuf:unittest_proto"],
)
